# BNF
## Queries by Original
```
<direct SQL statement> ::= <directly executable statement> ;
<directly executable statement> ::= <direct SQL data statement>
<direct SQL data statement> ::= <direct select statement: multiple rows>
<direct select statement: multiple rows> ::= <query expression> [ <order by clause> ]
<query expression> ::= <query expression body>
<query expression body> ::= <non-join query expression> | <joined table>
<non-join query expression> ::= <non-join query term>
<non-join query term> ::= <non-join query primary>
<non-join query primary> ::= <simple table>
<simple table> ::= <query specification>
<query specification> ::= SELECT [ <set quantifier> ] <select list> <table expression>
<select list> ::= * | <select sublist> [ { , <select sublist> }... ]
<select sublist> ::= <derived column> | <qualified asterisk>
<derived column> ::= <value expression> [ <as clause> ]
<as clause> ::= [ AS ] <column name>
<column name> ::= <IDENTIFIER>
<qualified asterisk> ::=  <asterisked identifier chain> <period> <asterisk> | <<all fields reference>
<asterisked identifier chain> ::= <asterisked identifier> [ { . <asterisked identifier> }... ]
<asterisked identifier> ::= <IDENTIFIER>
<all fields reference> ::= <value expression primary> . *
<value expression primary> ::= <parenthesized value expression> | <nonparenthesized value expression primary>
<parenthesized value expression> ::= ( <value expression> )
<nonparenthesized value expression primary> ::= <unsigned value specification> | <column reference> | <set function specification>
<unsigned value specification> ::= <unsigned literal> | <general value specification>
<unsigned literal> ::= <unsigned numeric literal> | <general literal>
<unsigned numeric literal> ::= <exact numeric literal> | <approximate numeric literal>
<approximate numeric literal> ::= <mantissa> E <exponent>
<mantissa> ::= <exact numeric literal>
<exponent> ::= <signed integer>
<signed integer> ::= [ <sign> ] <unsigned integer>
<exact numeric literal> ::=  <unsigned integer> [ . [ <unsigned integer> ] ] | . <unsigned integer>
<unsigned integer> ::= <digit> ...
<digit> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
<column reference> ::= <basic identifier chain>
<basic identifier chain> ::= <identifier chain>
<identifier chain> ::= <IDENTIFIER> [ { . <IDENTIFIER> }... ]
<set function specification> ::= COUNT ( * ) | <general set function>
<general set function> ::= <set function type> ( [ <set quantifier> ] <value expression> )
<set function type> ::= <computational operation>
<computational operation> ::= AVG | MAX | MIN | SUM | EVERY | ANY | SOME | COUNT
<set quantifier> ::= DISTINCT | ALL
<value expression> ::= <numeric value expression> | <string value expression> |  <boolean value expression> | <row value expression> | <reference value expression> | <collection value expression>
<numeric value expression> ::= <term> | <numeric value expression> + <term> | <numeric value expression> - <term>
<term> ::=  <factor> | <term> * <factor> | <term> / <factor>
<factor> ::= [ <sign> ] <numeric primary>
<numeric primary> ::= <value expression primary> | <numeric value function>
<sign> ::= + | -
<string value expression> ::= <character value expression> | <bit value expression> | <blob value expression>
<character value expression> ::= <concatenation> | <character factor>
<concatenation> ::= <character value expression> <concatenation operator> <character factor>
<character factor> := <character primary>
<character primary> ::= <value expression primary>
<bit value expression> ::= <bit concatenation> | <bit factor>
<bit concatenation> ::= <bit value expression> <concatenation operator> <bit factor>
<bit factor> ::= <bit primary>
<bit primary> ::= <value expression primary>
<blob value expression> ::= <blob concatenation> | <blob factor>
<blob concatenation> ::= <blob value expression> <concatenation operator> <blob factor>
<blob factor> ::= <blob primary>
<blob primary> ::= <value expression primary>
<numeric value function> ::= <absolute value expression>
<absolute value expression> ::= ABS ( <numeric value expression> )
<concatenation operator> ::= ||
<boolean value expression> ::= <boolean term> | <boolean value expression> OR <boolean term>
<boolean term> ::= <boolean factor> | <boolean term> AND <boolean factor>
<boolean factor> ::= [ NOT ] <boolean test>
<boolean test> ::= <boolean primary> [ IS [ NOT ] <truth value> ]
<boolean primary> ::= <predicate> | <parenthesized boolean value expression> | <nonparenthesized value expression primary>
<predicate> ::= <comparison predicate> | <between predicate> | <in predicate> | <null predicate> | <quantified comparison predicate> | <exists predicate> | <unique predicate>
<comparison predicate> ::= <row value expression> <comp op> <row value expression>
<row value expression> ::= <row value special case> | <row value constructor>
<row value special case> ::= <value specification> | <value expression>
<value specification> ::= <literal> | <general value specification>
<literal> ::= <signed numeric literal> | <general literal>
<signed numeric literal> ::= [ <sign> ] <unsigned numeric literal>
<row value constructor> ::= <row value constructor element>
<row value constructor element> ::= <value expression>
<between predicate> ::= <row value expression> [ NOT ] BETWEEN [ ASYMMETRIC | SYMMETRIC ] <row value expression> AND <row value expression>
<in predicate> ::= <row value expression> [ NOT ] IN <in predicate value>
<in predicate value> ::= <table subquery> | ( <in value list> )
<in value list> ::= <row value expression> [ { , <row value expression> }... ]
<null predicate> ::= <row value expression> IS [ NOT ] NULL
<quantified comparison predicate> ::= <row value expression> <comp op> <quantifier> <table subquery>
<quantifier> ::= <all> | <some>
<all> ::= ALL
<some> ::= SOME | ANY
<exists predicate> ::= EXISTS <table subquery>
<unique predicate> ::= UNIQUE <table subquery>
<comp op> ::= = | <> | < | > | <= | >=
<truth value> ::= TRUE | FALSE | UNKNOWN
<reference value expression> ::= <value expression primary>
<collection value expression> ::= <value expression primary>
<general value specification> ::= <SQL parameter reference> | <SQL variable reference>
<SQL parameter reference> ::= <basic identifier chain>
<SQL variable reference> ::= <basic identifier chain>
<general literal> ::= <character string literal> | <national character string literal> | <bit string literal> | <hex string literal> | <binary string literal> | <boolean literal>
<character string literal> ::= [ _ <character set specification> ] ' [ <character representation> ... ] ' [ { <separator> ' [ <character representation> ... ] ' }... ]
<character set specification> ::= <standard character set name> | <implementation-defined character set name> | <user-defined character set name>
<standard character set name> ::= <character set name>
<character set name> ::= [ <schema name> . ] <SQL language identifier>
<schema name> ::= [ <catalog name> . ] <unqualified schema name>
<catalog name> ::= <IDENTIFIER>
<unqualified schema name> ::= <IDENTIFIER>
<SQL language identifier> ::= <SQL language identifier start> [ { _ | <SQL language identifier part> }... ]
<SQL language identifier start> ::= <simple Latin letter>
<SQL language identifier part> ::= <simple Latin letter> | <digit>
<simple Latin letter> ::=  <simple Latin upper case letter> | <simple Latin lower case letter>
<simple Latin upper case letter> ::= A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z
<simple Latin lower case letter> ::= a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x | y | z
<digit> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
<implementation-defined character set name> ::= <character set name>
<user-defined character set name> ::= <character set name>
<character representation> ::= <nonquote character> | ''
<national character string literal> ::= N ' [ <character representation> ... ] ' [ { <separator> ' [ <character representation> ... ] ' }... ]
<bit string literal> ::= B ' [ <bit> ... ] ' [ { <separator> ' [ <bit> ... ] ' }... ]
<bit> ::= 0 | 1
<hex string literal> ::= X ' [ <hexit> ... ] ' [ { <separator> ' [ <hexit> ... ] ' }... ]
<hexit> ::= <digit> | A | B | C | D | E | F | a | b | c | d | e | f
<binary string literal> ::= X ' [ { <hexit> <hexit> }... ] ' [ { <separator> ' [ { <hexit> <hexit> }... ] ' }... ]
<boolean literal> ::= TRUE | FALSE | UNKNOWN
<separator> ::= { <comment> | <white space>}...
<comment> ::= <simple comment> | <bracketed comment>
<simple comment> ::= <simple comment introducer> [ <comment character> ... ] <newline>
<simple comment introducer> ::= -- [ - ... ]
<comment character> ::= <nonquote character> | '
<newline> ::= \r | \n | \r\n
<bracketed comment> ::= /* <bracketed comment contents> */
<bracketed comment contents> ::= [ { <comment character> | <separator> }... ]
<nonquote character> ::= !! See Character Sets
<table subquery> ::= <subquery>
<subquery> ::= ( <query expression> )
<table expression> ::= <from clause> [ <where clause> ] [ <group by clause> ] [ <having clause> ]
<from clause> ::= FROM <table reference list>
<table reference list> ::= <table reference> [ { , <table reference> }... ]
<table reference> ::= <table primary> | <joined table>
<table primary> ::= <table or query name> [ [ AS ] <correlation name> [ ( <derived column list> ) ] ] | <derived table> [ AS ] <correlation name> [ ( <derived column list> ) ] | ( <joined table> )
<table or query name> ::= <table name>
<table name> ::= <local or schema qualified name>
<local or schema qualified name> ::= [ <local or schema qualifier> . ] <qualified identifier>
<local or schema qualifier> ::= <schema name>
<qualified identifier> ::= <IDENTIFIER>
<correlation name> ::= <IDENTIFIER>
<derived column list> ::= <column name list>
<column name list> ::= <column name> [ { , <column name> }... ]
<derived table> ::= <table subquery>
<joined table> ::= <cross join> | <qualified join> | <union join>
<cross join> ::= <table reference> CROSS JOIN <table primary>
<qualified join> ::= <table reference> [ <join type> ] JOIN <table reference> <join specification>
<join type> ::= INNER | <outer join type> [ OUTER ]
<outer join type> ::= LEFT | RIGHT | FULL
<join specification> ::= <join condition> | <named columns join>
<join condition> ::= ON <search condition>
<named columns join> ::= USING ( <join column list> )
<join column list> ::= <column name list>
<search condition> ::= <boolean value expression>
<union join> ::= <table reference> UNION JOIN <table primary>
<where clause> ::= WHERE <search condition>
<group by clause> ::= GROUP BY <grouping element list>
<grouping element list> ::= <grouping element> [ { , <grouping element> }... ]
<grouping element> ::= <ordinary grouping set>
<ordinary grouping set> ::= <grouping column reference> | ( <grouping column reference list> )
<grouping column reference> ::= <column reference>
<grouping column reference list> ::=  <grouping column reference> [ { , <grouping column reference> }... ]
<having clause> ::= HAVING <search condition>
<order by clause> ::= ORDER BY <sort specification list>
<sort specification list> ::= <sort specification> [ { , <sort specification> }... ]
<sort specification> ::= <sort key> [ <ordering specification> ]
<sort key> ::= <value expression>
<ordering specification> ::= ASC | DESC
```

## Query
```
<SQL> ::= <SELECT Statement><FROM Statement>[<WHERE Statement>][<GROUP BY Statement>][<HAVING Statement>][<ORDER BY Statement>][<LIMIT Statement>]
<SELECT Statement> ::= SELECT <Column List>
<Column List> ::= * | <Column Detail>+
```
